plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.novelplatform'
version = '0.0.1-SNAPSHOT'
description = 'Spring Boot JPA 기반 소설 연재 사이트 구축 프로젝트'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/snapshot' }
}

def querydslGeneratedDir = layout.buildDirectory.dir('generated/querydsl')

sourceSets {
    main {
        java {
            srcDir querydslGeneratedDir
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorGeneratedSourcesDirectory = querydslGeneratedDir.get().asFile
}

tasks.named('clean') {
    doFirst {
        delete querydslGeneratedDir.get().asFile
    }
}

dependencies {
    // JPA / Web / Security / Validation / Thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // DB
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // 개발/테스트 도구
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Querydsl (Jakarta)
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api:2.1.1'
// AWS S3 의존성 제거
//    // 5주차: AWS S3 의존성 추가
//    // Spring Cloud AWS BOM(Bill of Materials) - 버전 관리를 위해 추가
//    implementation platform('io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1')
//    // S3 사용을 위한 starter
//    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
}

tasks.named('test') {
    useJUnitPlatform()
}
